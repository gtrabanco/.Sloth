#compdef dot lazy=dot s=lazy

. "${SLOTH_PATH:-${DOTLY_PATH:-}}/scripts/core/src/_main.sh"

_dot() {
  local curcontext="$curcontext" state line script_path
  typeset -A opt_args

  _arguments \
    '1: :->context' \
    '2: :->script' \
    '*: :->args'

  case $state in
  context)
    existing_contexts=$(dot::list_contexts)
    _arguments "1:Context:($existing_contexts)"
    ;;
  script)
    existing_scripts=$(dot::list_context_scripts "${words[2]}")
    _arguments "2:Script:($existing_scripts)"
    ;;
  args)
    script_path="${SLOTH_PATH:-${DOTLY_PATH:-}}/scripts/${words[2]}/${words[3]}"
    if [[ -r "$script_path" ]]; then
      existing_args="$(docs::parse_docopt_argument "$script_path" "${words[4]:-}")"
      if [[ -n "$existing_args" ]]; then
        _arguments "3:Args:($existing_args)"
      else
        _files
      fi
    else
      _files
    fi
    ;;
  *)
    _files
    ;;
  esac
}

_dot "$@"
